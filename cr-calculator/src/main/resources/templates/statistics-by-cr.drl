template header
challengeRating
proficiencyBonus
armorClass
minHitPoints
maxHitPoints
attackBonus
minDamage
maxDamage
saveDc

package templates

import com.sbnz.crcalculator.facts.Monster;
import com.sbnz.crcalculator.facts.Action;
import com.sbnz.crcalculator.facts.Attack;
import com.sbnz.crcalculator.facts.ChallengeRating;
import java.util.ArrayList;

template "statistics-by-cr"

	rule "Determine proficiency bonus for challenge rating @{challengeRating}"
		when
			$m: Monster(challengeRating.value.equals("@{challengeRating}")) @Watch(challengeRating)
		then
			System.out.println("Determine proficiency bonus for challenge rating @{challengeRating} fired");
			modify ($m) { setProficiencyBonus(@{proficiencyBonus}) };
	end

	rule "Determine offensive challenge rating for damage @{minDamage}-@{maxDamage}"
		when
			$m: Monster($actions: actions, offensiveChallengeRating == null)
			forall(Action(processed) from $actions)
			$n: Number(intValue >= @{minDamage}, intValue <= @{maxDamage}) from accumulate(
				$action: Action() from $actions,
				max($action.getAverageDamage())
			)
			$a: Action(averageDamage == $n.intValue()) from $actions
			ChallengeRating(value.equals("@{challengeRating}"), $o: ordinal)
			$cr: ChallengeRating(ordinal == $o + ($a.getMostCommonAttackBonus() - @{attackBonus})/2)
		then
			System.out.println("Determine offensive challenge rating for damage @{minDamage}-@{maxDamage} fired");
			modify ($m) {setOffensiveChallengeRating($cr)};
	end
	
	rule "Determine defensive challenge rating for hit points @{minHitPoints}-@{maxHitPoints}"
		when
			$m: Monster(constitution.abilityModifier != null, hitDie != null, averageHitPoints >= @{minHitPoints}, averageHitPoints <= @{maxHitPoints}, defensiveChallengeRating == null, $ac: armorClass)
			ChallengeRating(value.equals("@{challengeRating}"), $o: ordinal)
			$cr: ChallengeRating(ordinal == $o + ($ac - @{armorClass})/2)
		then
			System.out.println("Determine defensive challenge rating for hit points @{minHitPoints}-@{maxHitPoints} fired");
			modify ($m) {setDefensiveChallengeRating($cr)}
	end
	
end template