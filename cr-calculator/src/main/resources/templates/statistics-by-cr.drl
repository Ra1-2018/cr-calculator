template header
challengeRating
proficiencyBonus
armorClass
minHitPoints
maxHitPoints
attackBonus
minDamage
maxDamage
saveDc

package templates

import com.sbnz.crcalculator.facts.Monster;
import com.sbnz.crcalculator.facts.Action;
import com.sbnz.crcalculator.facts.Attack;
import com.sbnz.crcalculator.service.ChallengeRatingService;
import java.util.ArrayList;

global ChallengeRatingService challengeRatingService;

template "statistics-by-cr"

	rule "Determine proficiency bonus for challenge rating @{challengeRating}"
		when
			$m: Monster(challengeRating.value.equals("@{challengeRating}")) @Watch(challengeRating)
		then
			System.out.println("Determine proficiency bonus for challenge rating @{challengeRating} fired");
			modify ($m) { setProficiencyBonus(@{proficiencyBonus}) };
	end

	rule "Determine offensive challenge rating for damage @{minDamage}-@{maxDamage}"
		when
			$m: Monster($actions: actions, offensiveChallengeRating == null)
			forall(Action() from $actions)
			$attacks: ArrayList() from accumulate(
				Action(
					$as: attacks
				) from $actions,
				init(ArrayList list = new ArrayList();),
				action(list.addAll($as);),
				result(list)
			)
			forall(Attack(attackBonus != null, damageBonus != null) from $attacks)
			$n: Number(intValue >= @{minDamage}, intValue <= @{maxDamage}) from accumulate(
				$action: Action(
					averageDamage != null
				) from $actions,
				max($action.getAverageDamage())
			)
			$a: Action(averageDamage == $n.intValue()) from $actions
		then
			System.out.println("Determine offensive challenge rating for damage @{minDamage}-@{maxDamage} fired");
			Integer ordinal = challengeRatingService.findByValue("@{challengeRating}").getOrdinal() + (Integer)(($a.getMostCommonAttackBonus() - @{attackBonus})/2);
			modify ($m) {setOffensiveChallengeRating(challengeRatingService.findByOrdinal(ordinal))};
	end
	
	rule "Determine defensive challenge rating for hit points @{minHitPoints}-@{maxHitPoints}"
		when
			$m: Monster(constitution.abilityModifier != null, hitDie != null, averageHitPoints >= @{minHitPoints}, averageHitPoints <= @{maxHitPoints}, defensiveChallengeRating == null, $ac: armorClass)
		then
			System.out.println("Determine defensive challenge rating for hit points @{minHitPoints}-@{maxHitPoints} fired");
			Integer ordinal = challengeRatingService.findByValue("@{challengeRating}").getOrdinal() + (Integer)(($ac - @{armorClass})/2);
			modify ($m) {setDefensiveChallengeRating(challengeRatingService.findByOrdinal(ordinal))}
	end
	
end template