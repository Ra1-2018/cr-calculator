package rules

import com.sbnz.crcalculator.facts.Monster;
import com.sbnz.crcalculator.facts.ChallengeRating;

rule "Determine challenge rating" salience -1
	    when
	        $m: Monster() @Watch(actions)
	        finalChallengeRating($cr;)
	    then
	    	System.out.println("Challenge rating calculated to " + $cr.getValue());
	        modify ($m) { setChallengeRating($cr) };
end

query finalChallengeRating(ChallengeRating $cr)
	$m: Monster()
	offensiveChallengeRating($off;)
	defensiveChallengeRating($def;)
	getChallengeRating($cr, ($off.getOrdinal() + $def.getOrdinal())/2, $m.getChallengeRating();)
end

query getChallengeRating(ChallengeRating $cr, Integer expectedOrdinal, ChallengeRating currentChallengeRating)
	$cr := ChallengeRating(ordinal == expectedOrdinal, this != currentChallengeRating)
end

query maxDamageAction(Action $action)
	Monster($actions: actions)
	$action := Action($avg: averageDamage) from $actions
	not Action(averageDamage > $avg) from $actions
end

query highestAttackBonus(Action action, Integer $attackBonus)
	Attack($attackBonus := attackBonus) from action.getAttacks()
	not (Attack(attackBonus > $attackBonus) from action.getAttacks())
end

query offensiveChallengeRating(ChallengeRating $cr)
	maxDamageAction($action;)
	highestAttackBonus($action, $attackBonus;)
	ChallengeRating(minDamage <= $action.getAverageDamage(), maxDamage >= $action.getAverageDamage(), $ab: attackBonus, $o: ordinal)
	getChallengeRating($cr, $o + ($attackBonus - $ab)/2, null;)
end

query defensiveChallengeRating(ChallengeRating $cr)
	$m: Monster($hp: averageHitPoints)
	ChallengeRating(minHitPoints <= $hp, maxHitPoints >= $hp, $ac: armorClass, $o: ordinal)
	getChallengeRating($cr, $o + ($m.getArmorClass() - $ac)/2, null;)
end